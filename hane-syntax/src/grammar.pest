commands = _{ SOI ~ command* ~ EOI }
command = _{ command_definition | command_axiom | command_inductive | command_print | command_check | command_compute }
command_definition = {
    keyword_definition ~ ident ~ binders ~ ":" ~ expr ~ ":=" ~ expr ~ "."
}
command_axiom = { keyword_axiom ~ ident ~ ":" ~ expr ~ "." }
command_inductive = { keyword_inductive ~ inductive_body ~ (keyword_with ~ inductive_body)* ~ "." }
command_print = { keyword_print ~ ident ~ "." }
command_check = { keyword_check ~ expr ~ "." }
command_compute = { keyword_compute ~ expr ~ "." }

inductive_body = { ident ~ binders ~ ":" ~ expr ~ ":=" ~ inductive_constructors }
inductive_constructors = { inductive_constructor? ~ ("|" ~ inductive_constructor)* }
inductive_constructor = { ident ~ ":" ~ expr }

expr = { expr_inner+ }
expr_inner = _{
    expr_paren | sort | expr_var | expr_product | expr_abstract | expr_let_bind |
    expr_match | expr_fix
}
expr_paren = { "(" ~ expr ~ ")" }
expr_var = { ident }
expr_product = { keyword_forall ~ open_binders ~ "," ~ expr }
expr_abstract = { keyword_fun ~ open_binders ~ "=>" ~ expr }
expr_let_bind = {
    keyword_let ~ ident ~ ":" ~ expr ~ ":=" ~ expr ~ keyword_in ~ expr
}
expr_match = {
    keyword_match ~ expr ~ keyword_as ~ ident ~ keyword_in ~ pattern ~ keyword_return ~ expr ~
    keyword_with ~ expr_match_arms ~ keyword_end
}
expr_match_arms = { expr_match_arm? ~ ("|" ~ expr_match_arm)* }
expr_match_arm = { pattern ~ "=>" ~ expr }
pattern = { ident ~ ident* }

expr_fix = { keyword_fix ~ expr_fix_decl ~ expr_fix_with? }
expr_fix_with = { expr_fix_decls ~ keyword_for ~ ident }
expr_fix_decls = { (keyword_with ~ expr_fix_decl)+ }
expr_fix_decl = { ident ~ binders ~ "{" ~ keyword_struct ~ ident ~ "}" ~ ":" ~ expr ~ ":=" ~ expr }

binders = { binder* }
open_binders = { open_binder | binder+ }
binder = _{ "(" ~ open_binder ~ ")" }
open_binder = { ident ~ ":" ~ expr}


sort = { sort_prop | sort_set | sort_type }
sort_prop = { keyword_prop }
sort_set = { keyword_set }
sort_type = { keyword_type ~ "{" ~ number ~ "}" }

keyword = _{
    keyword_definition
   | keyword_axiom
   | keyword_inductive
   | keyword_print
   | keyword_check
   | keyword_compute
   | keyword_with
   | keyword_prop
   | keyword_set
   | keyword_type
   | keyword_forall
   | keyword_fun
   | keyword_let
   | keyword_in
   | keyword_match
   | keyword_return
   | keyword_as
   | keyword_end
   | keyword_fix
   | keyword_struct
   | keyword_for
}
keyword_definition = @{ "Definition" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_axiom = @{ "Axiom" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_inductive = @{ "Inductive" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_print = @{ "Print" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_check = @{ "Check" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_compute = @{ "Compute" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_with = @{ "with" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_prop = @{ "Prop" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_set = @{ "Set" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_type = @{ "Type" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_forall = @{ "forall" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_fun = @{ "fun" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_let = @{ "let" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_in = @{ "in" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_match = @{ "match" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_return = @{ "return" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_as = @{ "as" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_end = @{ "end" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_fix = @{ "fix" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_struct = @{ "struct" ~ !(ASCII_ALPHANUMERIC | "_") }
keyword_for = @{ "for" ~ !(ASCII_ALPHANUMERIC | "_") }

number = @{ ASCII_DIGIT+ }

ident = @{ !keyword ~ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\n" | "\r" }
COMMENT = _{ "(*" ~ (!"*)" ~ (COMMENT | ANY))* ~ "*)" }
